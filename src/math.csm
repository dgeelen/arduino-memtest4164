/******************************************************************************

   This file contains math functions.

 ******************************************************************************/
#pragma once

#include "abi.csm"
#include "utility_macros.csm"
#include "instructions.csm"
#include <boost/preprocessor/cat.hpp>


/**
 * Given a (constant) size of an integer type, returns how many bytes would be
 * needed to encode the largest representable number.
 */
#define bcd_encoded_length_bytes(n) \
	/* two BCD decimals can be stored in one byte */ \
	((BOOST_PP_CAT(__bcd_encoded_length_decimal_places_, n)+1)/2)


/**
 * Given a (constant) size of an integer type, returns how many decimal places
 * would be needed to encode the largest representable number.
 */
#define __bcd_encoded_length_decimal_places_0  0
#define __bcd_encoded_length_decimal_places_1  3
#define __bcd_encoded_length_decimal_places_2  5
#define __bcd_encoded_length_decimal_places_3  8
#define __bcd_encoded_length_decimal_places_4 10
#define bcd_encoded_length_decimal_places(n) BOOST_PP_CAT(__bcd_encoded_length_decimal_places_, n)


; in: X points to char/short/int
;     r16 indicates length of X
;     Y points to output buffer of bcd_encoded_length(r16)
;
; reference: https://en.wikipedia.org/wiki/Double_dabble
DEF_LABELED(bin2bcd,                                                            $\
	save_registers(                                                                \
		r22, r23,                                                                    \
		zl, zh                                                                       \
	)                                                                             $\
	                                                                              $\
	; reserve some scratch space                                                  $\
	stack_reserve(4, r25)                                                         $\
	                                                                              $\
	; copy input to scratch                                                       $\
	in     zl, spl                                                                $\
	in     zh, sph                                                                $\
	ldi    r24, 4+1       ; input needs to be placed at the 'end' of the scratch, $\
	sub    r24, r16       ; plus one because SP points to first free byte         $\
	add    zl, r24                                                                $\
	adc    zh, rC0                                                                $\
	mov    r24, r16                                                               $\
__bin2bcd_copy_src:                                                             $\
	ld     r25, x+                                                                $\
	st     z+, r25                                                                $\
	dec    r24                                                                    $\
	brne   __bin2bcd_copy_src                                                     $\
	sub    xl, r16       ; restore x                                              $\
	sbc    xh, rC0                                                                $\
	                                                                              $\
	; figure out length of result                                                 $\
	ldi    zl, low(2*__bin2bcd_dst_bytes_lookup)                                  $\
	ldi    zh, high(2*__bin2bcd_dst_bytes_lookup)                                 $\
	add    zl, r16                                                                $\
	adc    zh, rC0                                                                $\
	lpm    r23, z                                                                 $\
	                                                                              $\
	mov    r24, r23                                                               $\
__bin2bcd_clear_dst:                                                            $\
	st     y+, rC0                                                                $\
	dec    r24                                                                    $\
	brne   __bin2bcd_clear_dst                                                    $\
	sub    yl, r23       ; restore y                                              $\
	sbc    yh, rC0                                                                $\
	                                                                              $\
	mov    r22, r16      ; how many bits to process total                         $\
	lsl    (r22, 3)      ; N*8                                                    $\
	                                                                              $\
__bin2bcd:                                                                      $\
	; adjust BCD (separate step because this clobbers carry)                      $\
	; needs to be done _before_ shifting                                          $\
	; Note:                                                                       $\
	; this leaves Y at the end of the output, which is just what is needed for    $\
	; the shift (rtl) below. How convenient!                                      $\
	mov    r24, r23                                                               $\
__bin2bcd_bcd_adjust_output:                                                    $\
	ld     zl, y                                                                  $\
	mov    zh, zl                                                                 $\
	andi   zl, 0x0f                                                               $\
	cpi    zl, 0x05     ; if > 4                                                  $\
	brlo   __bin2bcd_bcd_adjust_no_add_lo                                         $\
	subi   zl, -0x03    ; add 3                                                   $\
__bin2bcd_bcd_adjust_no_add_lo:                                                 $\
	andi   zh, 0xf0                                                               $\
	cpi    zh, 0x50     ; if > 4                                                  $\
	brlo   __bin2bcd_bcd_adjust_no_add_hi                                         $\
	subi   zh, -0x30    ; add 3                                                   $\
__bin2bcd_bcd_adjust_no_add_hi:                                                 $\
	or     zl, zh                                                                 $\
	st     y+, zl                                                                 $\
	dec    r24                                                                    $\
	brne   __bin2bcd_bcd_adjust_output                                            $\
	                                                                              $\
	; shift input                                                                 $\
	in     zl, spl                                                                $\
	in     zh, sph                                                                $\
	adiw   zl, 5         ; +4, but pre-decrement below. Note that because we use  $\
	mov    r24, r16      ; SP, an addition is always required here.               $\
	clc                                                                           $\
__bin2bcd_shift_input:                                                          $\
	ld     r25, -z                                                                $\
	rol    r25                                                                    $\
	st     z, r25                                                                 $\
	dec    r24           ; careful with the carry...                              $\
	brne   __bin2bcd_shift_input ; we shifted all input bytes                     $\
	                                                                              $\
	; shift input msb (now in carry) into BCD                                     $\
	mov    r24, r23                                                               $\
__bin2bcd_shift_output:                                                         $\
	ld     r25, -y                                                                $\
	rol    r25                                                                    $\
	st     y, r25                                                                 $\
	dec    r24           ; careful with the carry...                              $\
	brne   __bin2bcd_shift_output ; we shifted all output bytes                   $\
	                                                                              $\
	; next round                                                                  $\
	dec    r22                                                                    $\
	brne   __bin2bcd                                                              $\
	                                                                              $\
__bin2bcd_done:                                                                 $\
	stack_free(4, r25)                                                            $\
	restore_registers(                                                             \
		r22, r23,                                                                    \
		zl, zh                                                                       \
	)                                                                             $\
	ret                                                                           $\
	                                                                              $\
__bin2bcd_dst_bytes_lookup: ; for speed                                         $\
	.db bcd_encoded_length_bytes(0),                                               \
	    bcd_encoded_length_bytes(1),                                               \
	    bcd_encoded_length_bytes(2),                                               \
	    bcd_encoded_length_bytes(3),                                               \
	    bcd_encoded_length_bytes(4),                                               \
	    bcd_encoded_length_bytes(0), /* dummy */                                   $\
	                                                                              $\
__bin2bcd_dst_nibbles_lookup: ; for accuracy                                    $\
	.db bcd_encoded_length_decimal_places(0),                                      \
	    bcd_encoded_length_decimal_places(1),                                      \
	    bcd_encoded_length_decimal_places(2),                                      \
	    bcd_encoded_length_decimal_places(3),                                      \
	    bcd_encoded_length_decimal_places(4),                                      \
	    bcd_encoded_length_decimal_places(0), /* dummy */                         $\
)


/**
 * Input:
 *   r16 no. bytes
 * output:
 *   r25 no. nibbles
 */
DEF_LABELED(bcd2bin_nibbles,                                                    $\
	save_registers(zl, zh)                                                        $\
	                                                                              $\
	ldi    zl, low(2*__bin2bcd_dst_nibbles_lookup)                                $\
	ldi    zh, high(2*__bin2bcd_dst_nibbles_lookup)                               $\
	add    zl, r16                                                                $\
	adc    zh, rC0                                                                $\
	lpm    r25, z                                                                 $\
	                                                                              $\
	restore_registers(zl, zh)                                                     $\
	ret                                                                           $\
)


/**
 * Input:
 *   r16 no. bytes
 * output:
 *   r25 no. bytes
 */
DEF_LABELED(bcd2bin_bytes,                                                      $\
	save_registers(zl, zh)                                                        $\
	                                                                              $\
	ldi    zl, low(2*__bin2bcd_dst_bytes_lookup)                                  $\
	ldi    zh, high(2*__bin2bcd_dst_bytes_lookup)                                 $\
	add    zl, r16                                                                $\
	adc    zh, rC0                                                                $\
	lpm    r25, z                                                                 $\
	                                                                              $\
	restore_registers(zl, zh)                                                     $\
	ret                                                                           $\
)


/**
 * In:
 * X BCD sequence
 * r16 length (bytes? nibbles?)
 * Y output buffer
 */
DEF_LABELED(bcd2ascii,                                                          $\
	mov    r24, r16                                                               $\
__bcd2ascii:                                                                    $\
	ld     r25, x                                                                 $\
	lsr    (r25, 4)                                                               $\
	subi   r25, -'0'                                                              $\
  st     y+, r25                                                                $\
	ld     r25, x+                                                                $\
	andi   r25, 0x0f                                                              $\
	subi   r25, -'0'                                                              $\
	st     y+, r25                                                                $\
	dec    r24                                                                    $\
	brne   __bcd2ascii                                                            $\
	                                                                              $\
	; restore X and Y (2 cycles faster than push/pop)                             $\
	mov    r24, r16                                                               $\
	sub    xl, r16                                                                $\
	sbc    xh, rC0                                                                $\
	lsl    r24                                                                    $\
	sub    yl, r24                                                                $\
	sbc    yh, rC0                                                                $\
	                                                                              $\
	ret                                                                           $\
)


////////////////////////////////////////////////////////////////////////////////


; divides two unsigned 16-bit numbers pointed to by x and y (X/Y).
; Result is stored at Z, dividend:remainder (4 bytes).
;
; based on https://www.avrfreaks.net/forum/asm200-maths-apnotes?name=PNphpBB2&file=viewtopic&t=4716
DEF_LABELED(div_u16,                                                            $\
	save_registers(r16, r17, r18, r19, r20)                                       $\
	ldi   r20, 17      ; number of bits (iterations)                              $\
	                                                                              $\
	ld    r17, x+      ; msb                                                      $\
	ld    r16, x+      ; lsb                                                      $\
	sbiw  xl, 2                                                                   $\
	ld    r19, y+                                                                 $\
	ld    r18, y+                                                                 $\
	sbiw  yl, 2                                                                   $\
	clr   r25                                                                     $\
	sub   r24, r24     ; zero and clc                                             $\
	                                                                              $\
__div_u16_next_bit:                                                             $\
	; We shift bits from the dividend r16:r17 into the work area r24:r25, while   $\
	; at the same time we shift bits from the quotient into r16:r17.              $\
	rol   r16                                                                     /*  1 */$\
	rol   r17                                                                     /*  2 */$\
	                                                                              $\
	; After all 16 input bits are handled we are done. The exit check is placed   $\
	; between the rols because the first two rols record the bits of the          $\
	; quotient, and after the last bit is processed we dont need to shift any     $\
	; more bits into the work area. I.e. this yields a minor speed improvement.   $\
	dec   r20                                                                     /*  3 */$\
	breq  __div_u16_done                                                          /*  4 */$\
	                                                                              $\
	rol   r24                                                                     /*  5 */$\
	rol   r25                                                                     /*  6 */$\
	                                                                              $\
	; Try to subtract the divisor                                                 $\
	sub   r24, r18                                                                /*  7 */$\
	sbc   r25, r19                                                                /*  8 */$\
	                                                                              $\
	; I.e. if the subtraction succeeded (no borrow needed) the number in the work $\
	; area is at least as large as the divider, and so we should place a 1 into   $\
	; the quotient. Otherwise we need to undo the subtraction and  place a 0 into $\
	; the quotient.                                                               $\
	; Note that we are storing the inverse, and will flip the bits at the end.    $\
	brbc  SREG_C, __div_u16_next_bit                                              /*  9.5 */$\
	add   r24, r18                                                                /* 10.5 */$\
	adc   r25, r19                                                                /* 11.5 */$\
	rjmp  __div_u16_next_bit                                                      /* 12.5 */$\
	                                                                              $\
__div_u16_done:                                                                 $\
	eor   r16, rC2 ; quotient                                                     $\
	eor   r17, rC2                                                                $\
	st    z+, r17                                                                 $\
	st    z+, r16                                                                 $\
	st    z+, r19  ; remainder                                                    $\
	st    z+, r18                                                                 $\
	sbiw  zl, 4                                                                   $\
	restore_registers(r16, r17, r18, r19, r20)                                    $\
	ret                                                                           $\
) // div_u16


; divides two unsigned 32-bit numbers pointed to be x and y (X/Y).
; Result is stored at Z, dividend:remainder (8 bytes).
;
; Functionally identical to the above 16bit divide.
DEF_LABELED(div_u32,                                                            $\
	save_registers(                                                                \
		r08, r09, r10, r11                                                           \
		r12, r13, r14, r15                                                           \
		r16, r17, r18, r19                                                           \
	)                                                                             $\
	ldi   r24, 33      ; number of bits (iterations)                              $\
	                                                                              $\
	ld    r11, x+      ; msb                                                      $\
	ld    r10, x+                                                                 $\
	ld    r09, x+                                                                 $\
	ld    r08, x+      ; lsb                                                      $\
	sbiw  xl, 4                                                                   $\
	ld    r15, y+                                                                 $\
	ld    r14, y+                                                                 $\
	ld    r13, y+                                                                 $\
	ld    r12, y+                                                                 $\
	sbiw  yl, 4                                                                   $\
	clr   r19                                                                     $\
	clr   r18                                                                     $\
	clr   r17                                                                     $\
	sub   r16, r16     ; zero and clc                                             $\
	                                                                              $\
__div_u32_next_bit:                                                             $\
	rol   r08                                                          /*  1   */ $\
	rol   r09                                                          /*  2   */ $\
	rol   r10                                                          /*  3   */ $\
	rol   r11                                                          /*  4   */ $\
	dec   r24                                                          /*  5   */ $\
	breq  __div_u32_done                                               /*  6   */ $\
	rol   r16                                                          /*  7   */ $\
	rol   r17                                                          /*  8   */ $\
	rol   r18                                                          /*  9   */ $\
	rol   r19                                                          /* 10   */ $\
	sub   r16, r12                                                     /* 11   */ $\
	sbc   r17, r13                                                     /* 12   */ $\
	sbc   r18, r14                                                     /* 13   */ $\
	sbc   r19, r15                                                     /* 14   */ $\
	brbc  SREG_C, __div_u32_next_bit                                   /* 15.5 */ $\
	add   r16, r12                                                     /* 16.5 */ $\
	adc   r17, r13                                                     /* 17.5 */ $\
	adc   r18, r14                                                     /* 18.5 */ $\
	adc   r19, r15                                                     /* 19.5 */ $\
	rjmp  __div_u32_next_bit                                           /* 20.5 */ $\
	                                                                              $\
__div_u32_done:                                                                 $\
	eor   r08, rC2 ; quotient                                                     $\
	eor   r09, rC2                                                                $\
	eor   r10, rC2                                                                $\
	eor   r11, rC2                                                                $\
	st    z+, r11                                                                 $\
	st    z+, r10                                                                 $\
	st    z+, r09                                                                 $\
	st    z+, r08                                                                 $\
	st    z+, r19                                                                 $\
	st    z+, r18                                                                 $\
	st    z+, r17                                                                 $\
	st    z+, r16                                                                 $\
	sbiw  zl, 8                                                                   $\
	restore_registers(                                                             \
		r08, r09, r10, r11                                                           \
		r12, r13, r14, r15                                                           \
		r16, r17, r18, r19                                                           \
	)                                                                             $\
	ret                                                                           $\
) // div_u32
